plugins {
    id 'org.quiltmc.loom' version "1.1.+"

    id 'com.oroarmor.general-gradle-plugin' version '1.2.3'
    id 'com.oroarmor.minecraft-gradle-plugin' version '1.2.3'
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = "OroArmor"
        url = "https://maven.oroarmor.com"
    }
    maven {
        name = "Jitpack"
        url = 'https://jitpack.io'
    }
    maven {
        name = "Shedaniel"
        url = 'https://maven.shedaniel.me'
    }
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/releases/"
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
}

oroarmor {
    name = "Netherite Plus Mod"
    githubProjectName = "OroArmorModding/Netherite-Plus-Mod"
    description = "This mod adds extra netherite features that aren't vanilla.\n\nNOTE: Editing the config requires a game restart."
}

import com.oroarmor.orogradleplugin.minecraft.dependency.ModDependency.DependencyType
minecraftPublishing {
    gameVersions = ["1.19.4"]
    modrinthId = "MlaM22M4"
    curseforgeId = "394120"
    dependencies {
        "multi-item-lib" {
            version = "1.6.2"
            type = DependencyType.REQUIRED
        }
    }
    loaders = ["Quilt"]
    modTask = remapJar
}

license {
    exclude '**/*.accesswidener'
}

loom {
    accessWidenerPath = file("src/main/resources/netherite_plus.accesswidener")
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "org.quiltmc:quilt-mappings:${project.mappings}:intermediary-v2"
    modImplementation "org.quiltmc:quilt-loader:${project.quilt_loader_version}"

    modImplementation "org.quiltmc:qsl:${project.qsl_version}"
    modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${project.qfapi_version}"

    modImplementation "com.oroarmor:multi-item-lib:1.6.2"
    modCompileOnly('com.github.CrimsonDawn45:Fabric-Shield-Lib:main-SNAPSHOT')
    modRuntimeOnly("com.terraformersmc:modmenu:7.2.1")
}

processResources {
    inputs.property "version", version

    filesMatching('quilt.mod.json') {
        expand "version": version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            oroarmor.generateDefaultPom(it)

            archivesBaseName = project.archivesBaseName
        }
    }

    repositories {
        mavenLocal()
    }
}

import com.oroarmor.orogradleplugin.publish.PublishProjectToGithubTask
task github(type: PublishProjectToGithubTask) {
    assets.from(remapJar, sourcesJar)
}

import com.oroarmor.orogradleplugin.minecraft.*
task curseforge(type: CurseforgePublishTask)
task modrinth(type: ModrinthPublishTask)
